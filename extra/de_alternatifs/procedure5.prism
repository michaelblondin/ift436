dtmc

module systeme


a  : [-1..1] init  0; //  0 = pas choisi, {-1, 1} = valeur choisie
b  : [-1..1] init -1; // -1 = pas choisi,  {0, 1} = valeur du bit
i1 : [-1..1] init -1; // -1 = pas choisi,  {0, 1} = valeur du bit
i0 : [-1..1] init -1; // -1 = pas choisi,  {0, 1} = valeur du bit

// Choisi a et b
[] (a  = 0 & b = -1 & i1  = -1 & i0 = -1) -> 1/2 : (a' = -1) + 1/2 : (a' = 1);
[] (a != 0 & b = -1 & i1  = -1 & i0 = -1) -> 1/2 : (b' =  0) + 1/2 : (b' = 1);

// Choisir les deux bits de i ∈ {0, 1, 2}
[] (a != 0 & b != -1 & i1  = -1 & i0 = -1) -> 1/2 : (i1' = 0) + 1/2 : (i1' = 1);
[] (a != 0 & b != -1 & i1 != -1 & i0 = -1) -> 1/2 : (i0' = 0) + 1/2 : (i0' = 1);
[] (a != 0 & b != -1 & i1  =  1 & i0 =  1) -> (i1' = -1) & (i0' = -1);

endmodule

// Valeurs du dé
formula i = 2 * i1 + i0;
formula j = mod(i + a, 3);

formula termine = ((a != 0) & (b != -1) & (i0 != -1) & (i1 != -1) & ((i0 = 0) | (i1 = 0)));
formula valeur  = pow(2, i) + b * pow(2, j);

label "un"      = (termine & valeur = 1);
label "deux"    = (termine & valeur = 2);
label "trois"   = (termine & valeur = 3);
label "quatre"  = (termine & valeur = 4);
label "cinq"    = (termine & valeur = 5);
label "six"     = (termine & valeur = 6);

// Nombre de lancers
rewards "nombre_lancers"
(a = 0 | b = -1 | i1 = -1 | i0 = -1) : 1;
endrewards